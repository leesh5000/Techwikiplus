plugins {
    kotlin("jvm") version "1.9.25"
    kotlin("plugin.spring") version "1.9.25"
    kotlin("plugin.jpa") version "1.9.25"
    id("org.springframework.boot") version "3.5.4"
    id("io.spring.dependency-management") version "1.1.7"
    // RestDocs API Spec - Spring REST Docsì™€ OpenAPI í†µí•©
    id("com.epages.restdocs-api-spec") version "0.19.0"
    // ktlint: Kotlin ì½”ë“œ ìŠ¤íƒ€ì¼ ê²€ì‚¬ ë° í¬ë§·íŒ… ë„êµ¬
    // - ./gradlew ktlintCheck: ì½”ë“œ ìŠ¤íƒ€ì¼ ìœ„ë°˜ ê²€ì‚¬
    // - ./gradlew ktlintFormat: ìë™ ì½”ë“œ í¬ë§·íŒ…
    // - ./gradlew addKtlintCheckGitPreCommitHook: Git ì»¤ë°‹ ì „ ìë™ ê²€ì‚¬ ì„¤ì •
    id("org.jlleitschuh.gradle.ktlint") version "12.1.0"
}

group = "me.helloc"
version = "0.0.1-SNAPSHOT"
description = "techwikiplus"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Kotlin reflection - Spring Bootì™€ Kotlin data class ì‚¬ìš© ì‹œ í•„ìˆ˜
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    // Jackson Kotlin ëª¨ë“ˆ - JSON ì§ë ¬í™”/ì—­ì§ë ¬í™”
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

    // Spring Boot Web - REST API ê°œë°œ
    implementation("org.springframework.boot:spring-boot-starter-web")

    // Spring Boot Data JPA - ë°ì´í„°ë² ì´ìŠ¤ ì—°ë™
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")

    // Spring Security - ì¸ì¦/ì¸ê°€ ë° ë¹„ë°€ë²ˆí˜¸ ì•”í˜¸í™”
    implementation("org.springframework.boot:spring-boot-starter-security")

    // JWT
    implementation("io.jsonwebtoken:jjwt-api:0.11.5")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.5")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.5")

    // DB
    runtimeOnly("com.mysql:mysql-connector-j")

    // Flyway - ë°ì´í„°ë² ì´ìŠ¤ ë§ˆì´ê·¸ë ˆì´ì…˜
    implementation("org.flywaydb:flyway-core")
    implementation("org.flywaydb:flyway-mysql")

    // Spring Boot í…ŒìŠ¤íŠ¸ ì§€ì›
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    // Spring Security í…ŒìŠ¤íŠ¸ ì§€ì›
    testImplementation("org.springframework.security:spring-security-test")
    // WebTestClientë¥¼ ìœ„í•œ WebFlux ì˜ì¡´ì„±
    testImplementation("org.springframework.boot:spring-boot-starter-webflux")
    // TestContainers í•µì‹¬ ë¼ì´ë¸ŒëŸ¬ë¦¬
    testImplementation("org.testcontainers:testcontainers")
    // JUnit5 í†µí•©ì„ ìœ„í•œ TestContainers í™•ì¥
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation("org.testcontainers:junit-jupiter")

    // kotest
    testImplementation("io.kotest:kotest-runner-junit5:5.7.2")
    testImplementation("io.kotest:kotest-assertions-core:5.7.2")
    testImplementation("io.kotest:kotest-property:5.7.2")
    testImplementation("io.kotest.extensions:kotest-extensions-spring:1.1.3")

    // ArchUnit: ì•„í‚¤í…ì²˜ í…ŒìŠ¤íŠ¸
    testImplementation("com.tngtech.archunit:archunit-junit5:1.2.1")

    // MySQL ì»¨í…Œì´ë„ˆ - JPA Repository í†µí•© í…ŒìŠ¤íŠ¸ìš©
    testImplementation("org.testcontainers:mysql")

    // Spring REST Docs
    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")

    // RestDocs API Spec - Spring REST Docsì™€ OpenAPI í†µí•©
    testImplementation("com.epages:restdocs-api-spec-mockmvc:0.19.0")

    // Swagger UI - OpenAPI ë¬¸ì„œ ì‹œê°í™”
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0")

    // MailSender - ì´ë©”ì¼ ì „ì†¡ ê¸°ëŠ¥
    implementation("org.springframework.boot:spring-boot-starter-mail")

    // Redis - ìºì‹œ ì €ì¥ì†Œ
    implementation("org.springframework.boot:spring-boot-starter-data-redis")
    // Kotlin Coroutines - ë³‘ë ¬ ì²˜ë¦¬ë¥¼ ìœ„í•œ ì½”ë£¨í‹´
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")

    // MockK - Kotlin mocking library for testing
    testImplementation("io.mockk:mockk:1.13.8")

    // Spring Boot Actuator (í—¬ìŠ¤ì²´í¬, ë©”íŠ¸ë¦­)
    implementation("org.springframework.boot:spring-boot-starter-actuator")

    // Micrometer Prometheus - í”„ë¡œë©”í…Œìš°ìŠ¤ ë©”íŠ¸ë¦­ ë…¸ì¶œ
    implementation("io.micrometer:micrometer-registry-prometheus")
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict")
    }
}

tasks.withType<Test> {
    useJUnitPlatform()
}

// Spring Boot ë©”ì¸ í´ë˜ìŠ¤ ì§€ì •
springBoot {
    mainClass.set("me.helloc.techwikiplus.TechwikiplusApplicationKt")
}

ktlint {
    version.set("1.0.1")
    verbose.set(true)
    outputToConsole.set(true)
    outputColorName.set("RED")

    filter {
        exclude("**/generated/**")
        exclude("**/build/**")
    }

    reporters {
        reporter(org.jlleitschuh.gradle.ktlint.reporter.ReporterType.PLAIN)
        reporter(org.jlleitschuh.gradle.ktlint.reporter.ReporterType.CHECKSTYLE)
    }
}

// Git pre-commit hook ì„¤ì¹˜ íƒœìŠ¤í¬
tasks.register<Exec>("installGitHook") {
    description = "Install pre-commit git hook for ktlint"
    group = "git hooks"
    commandLine("sh", "-c", "./gradlew addKtlintCheckGitPreCommitHook")
}

// OpenAPI 3.0.1 ë¬¸ì„œ ìƒì„± ì„¤ì •
//
// ğŸ“Œ ì¤‘ìš”: OpenAPI ë¬¸ì„œ ê´€ë¦¬ ì •ì±…
// - ìƒì„±ëœ openapi3.yml íŒŒì¼ì€ Gitì— ì»¤ë°‹ë˜ì–´ ê´€ë¦¬ë¨
// - Docker ë¹Œë“œ ì‹œ ì„±ëŠ¥ ìµœì í™”ë¥¼ ìœ„í•´ í…ŒìŠ¤íŠ¸ë¥¼ ê±´ë„ˆë›°ë¯€ë¡œ (-x test)
//   ì‚¬ì „ì— ìƒì„±ëœ ë¬¸ì„œê°€ JARì— í¬í•¨ë˜ì–´ì•¼ í•¨
// - API ë³€ê²½ ì‹œ ê°œë°œìëŠ” ë°˜ë“œì‹œ:
//   1. ./gradlew test ì‹¤í–‰í•˜ì—¬ ë¬¸ì„œ ì¬ìƒì„±
//   2. src/main/resources/static/api-docs/openapi3.yml íŒŒì¼ ì»¤ë°‹
// - ìì„¸í•œ ë‚´ìš©ì€ src/main/resources/static/api-docs/README.md ì°¸ì¡°
openapi3 {
    // ì„œë²„ ì„¤ì • - ê¸°ë³¸ê°’ ì‚¬ìš© (http://localhost)
    // TODO: ë‹¤ì¤‘ ì„œë²„ ì„¤ì • êµ¬í˜„ í•„ìš”
    setServer("http://localhost:9000")

    title = "TechWikiPlus API"
    description = "TechWikiPlus API Documentation"
    version = System.getenv("IMAGE_TAG") ?: "LOCAL_VERSION"
    format = "yml"
    outputDirectory = "build/api-spec"
    snippetsDirectory = "build/generated-snippets"
}

// OpenAPI ë¬¸ì„œì— ë‹¤ì¤‘ ì„œë²„ ì„¤ì •ì„ ì¶”ê°€í•˜ëŠ” íƒœìŠ¤í¬
tasks.register("updateOpenApiServers") {
    dependsOn("openapi3")
    doLast {
        val openApiFile = file("build/api-spec/openapi3.yml")
        if (openApiFile.exists()) {
            val content = openApiFile.readText()
            // ë‹¨ì¼ ì„œë²„ë¥¼ ë‹¤ì¤‘ ì„œë²„ë¡œ êµì²´
            val updatedContent =
                content.replace(
                    Regex("servers:\\s*\\n\\s*- url: http://localhost:9000"),
                    """servers:
  - url: http://localhost:9000
    description: Local server
  - url: http://13.124.188.47:9000
    description: Production server""",
                )
            openApiFile.writeText(updatedContent)
        }
    }
}

// OpenAPI ë¬¸ì„œë¥¼ ì •ì  ë¦¬ì†ŒìŠ¤ë¡œ ë³µì‚¬í•˜ëŠ” íƒœìŠ¤í¬
// í…ŒìŠ¤íŠ¸ ì‹¤í–‰ í›„ ìë™ìœ¼ë¡œ ì‹¤í–‰ë˜ì–´ ë¬¸ì„œë¥¼ ë¦¬ì†ŒìŠ¤ ë””ë ‰í† ë¦¬ì— ë³µì‚¬
// ê°œë°œìëŠ” ì´ íŒŒì¼ì„ Gitì— ì»¤ë°‹í•´ì•¼ í•¨
tasks.register<Copy>("copyOpenApiToResources") {
    dependsOn("updateOpenApiServers")
    from("build/api-spec/openapi3.yml")
    into("src/main/resources/static/api-docs")

    // ë³µì‚¬ ì „ ëŒ€ìƒ ë””ë ‰í† ë¦¬ ì •ë¦¬
    doFirst {
        delete("src/main/resources/static/api-docs/openapi3.yml")
    }
}

// í…ŒìŠ¤íŠ¸ ì„¤ì •
tasks.test {
    useJUnitPlatform()

    // í…ŒìŠ¤íŠ¸ ì‹¤í–‰ ì „ ê¸°ì¡´ ìŠ¤ë‹ˆí«ë§Œ ì •ë¦¬
    // OpenAPI ë¬¸ì„œëŠ” ì‚­ì œí•˜ì§€ ì•Šê³  ëˆ„ì ë˜ë„ë¡ í•¨
    doFirst {
        delete("build/api-spec")
        delete("build/generated-snippets")
        // ìµœì¢… ë¬¸ì„œëŠ” ì‚­ì œí•˜ì§€ ì•ŠìŒ - openapi3 íƒœìŠ¤í¬ê°€ ë³‘í•© ì²˜ë¦¬
    }

    // Java 21+ ê²½ê³  ë©”ì‹œì§€ ì œê±°
    jvmArgs(
        // ë™ì  ì—ì´ì „íŠ¸ ë¡œë”© ëª…ì‹œì  í—ˆìš©
        "-XX:+EnableDynamicAgentLoading",
        // í´ë˜ìŠ¤ ë°ì´í„° ê³µìœ  ë¹„í™œì„±í™”ë¡œ bootstrap classpath ê²½ê³  ì œê±°
        "-Xshare:off",
    )

    testLogging {
        events("passed", "skipped", "failed")
        showStandardStreams = false
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
    }

    // í…ŒìŠ¤íŠ¸ ì‹¤í–‰ í›„ ìë™ìœ¼ë¡œ OpenAPI ë¬¸ì„œ ìƒì„± ë° ë³µì‚¬
    // openapi3 íƒœìŠ¤í¬ë¥¼ ëª…ì‹œì ìœ¼ë¡œ ì‹¤í–‰í•˜ë„ë¡ ìˆ˜ì •
    finalizedBy("openapi3", "copyOpenApiToResources")
}
